# Made by Naman Singh, July 2024

import basic.osc

#scenario to implement ego vehicle overtaking:

#we have 3 cars:ego, lead1, npc1
# npc1 is in the right of ego hence ego can't overtake lead1 directly
# ego wants to overtake the lead1 vehicle as it is moving slowly

scenario top:
    path: Path                      # A path in the map
    path.set_map("Highway_Map_Light")    # specify map to use
    path.path_min_driving_lanes(3)         # Path should have at least 3 lanes

    ego_vehicle: Model3  
    lead1_vehicle: Creta
    npc1: Mercedes
    
    event start
    event end
    event left
    event right

    do serial:

        spawn: parallel(duration: 8s):
            #the 3 vehicles are spawned and begin their drive
            ego_vehicle.drive(path) with:
                speed(20mps)
                lane(1, at:start)
            lead1_vehicle.drive(path) with:
                speed(10mps)
                lane(same_as:ego_vehicle, at:start)
                position(60m, ahead_of:ego_vehicle, at:start)
            npc1.drive(path) with:
                speed(20mps)
                lane(right_of:ego_vehicle, at:start)
                position(5m, behind:ego_vehicle, at:start)
        slow_down: parallel(duration: 4s):
            #ego_vehicle slows down before overtake
            #to match the speed of lead1 vehicle and 
            #allow npc1 to pass through the right lane
            ego_vehicle.drive(path) with:
                speed(10mps)
            lead1_vehicle.drive(path) with:
                speed(10mps)
            npc1.drive(path) with:
                speed(20mps) 
        lanechange_right: parallel(duration: 3s):
            ego_vehicle.drive(path) with:
                change_lane(lane_changes:1,side:right)
            lead1_vehicle.drive(path) with:
                speed(10mps)
            npc1.drive(path) with:
                speed(20mps)  
        speedup: parallel(duration: 4s):
            ego_vehicle.drive(path) with:
                speed(20mps)
            lead1_vehicle.drive(path) with:
                speed(10mps)
            npc1.drive(path) with:
                speed(20mps) 
        lanechange_left: parallel(duration: 3s):
            ego_vehicle.drive(path) with:
                change_lane(lane_changes:1,side:left)
            lead1_vehicle.drive(path) with:
                speed(10mps)
            npc1.drive(path) with:
                speed(20mps) 
        drive_freely: parallel(duration: 4s):
            ego_vehicle.drive(path) with:
                speed(20mps)
            lead1_vehicle.drive(path) with:
                speed(10mps)
            npc1.drive(path) with:
                speed(20mps) 